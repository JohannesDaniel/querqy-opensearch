apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'idea'
apply plugin: 'opensearch.opensearchplugin'

opensearchplugin {
    name 'querqy-opensearch'
    description 'Query rewriting plugin'
    classname 'querqy.opensearch.QuerqyPlugin'
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:1.0.0-beta1"
    }
}

repositories {
    mavenLocal()
}

configurations.all {
    resolutionStrategy {
        force 'com.fasterxml.jackson.core:jackson-databind:2.10.4'
    }
}

// TODO: versions to variables
dependencies {
    compile('org.querqy:querqy-lucene:5.1.lucene810.0') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }

    compile('com.fasterxml.jackson.core:jackson-databind')
    compile('com.jayway.jsonpath:json-path:2.4.0')

    testImplementation('org.apache.logging.log4j:log4j-core:2.11.1')
    testImplementation('org.assertj:assertj-core:3.15.0')
}

test {
    useJUnit()
    finalizedBy jacocoTestReport
    include "**/*Test.class"
    systemProperties 'java.security.policy': file("$projectDir/plugin-security.policy").absolutePath
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

// TODO: Cleanup code base to enable checks
forbiddenApisTest.enabled = false
checkstyleTest.enabled = false

dependencyLicenses.enabled = false
licenseHeaders.enabled = false
thirdPartyAudit.enabled = false

loggerUsageCheck.enabled = false
testingConventions.enabled = false
validateNebulaPom.enabled = false

